plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.saadahmedev.base'
version = '0.0.5'
def artifactVersion = version

java {
    sourceCompatibility = '17'

    withJavadocJar()
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def springBootVersion = '3.2.1'

dependencies {
    // Spring Web
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    // Spring Security
    implementation "org.springframework.boot:spring-boot-starter-security:$springBootVersion"

    // Spring Data JPA
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

    // JWT
    def jwtVersion = '0.11.5'
    implementation "io.jsonwebtoken:jjwt-api:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-impl:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-jackson:$jwtVersion"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.saadahmedev.base'
            artifactId = 'base-boot'
            version = artifactVersion

            artifact(javadocJar)
            artifact(sourcesJar)
            artifact(jar)

            pom {
                name = 'Base Spring Boot'
                description = 'Base Spring Boot service is created to simplify code base and gives a lot of boilerplate codes'
                url = 'https://github.com/saadahmedscse/base-spring-boot'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'saadahmedscse'
                        name = 'Saad Ahmed'
                        email = 'saadahmedscse@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/saadahmedscse/base-spring-boot.git'
                    developerConnection = 'scm:git:ssh://github.com:saadahmedscse/base-spring-boot.git'
                    url = 'https://github.com/saadahmedscse/base-spring-boot'
                }
            }
        }
    }

    repositories {
        maven {
            def releaseUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = artifactVersion.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username = "username"
                password = "password"
            }
        }
    }
}